/**
 * S-expression parser.
 *
 * ./bin/syntax -g parser/s-expression.cpp.bnf -m LALR1 -o ~/SExpressionParser.h
 *
 * Examples:
 *
 * Atom: 42, foo, bar, "Hello World"
 *
 * List: (), (+ 5 x), (print "hello")
 *
 * Usage:
 *
 *  string exp{R"( (var x (+ "hello" 10)) )"};
 *
 *  SExpressionParser parser;
 *
 *  auto ast = parser.parse(exp);
 *
 *  cout << "(";
 *  cout << ast->list[0]->symbol << " "; // var
 *  cout << ast->list[1]->symbol << " ("; // x (
 *  cout << ast->list[2]->list[0]->string << " "; // +
 *  cout << ast->list[2]->list[1]->string << " "; // "hello"
 *  cout << ast->list[2]->list[2]->number << "))\n"; 10 ))
 *
 * Result: (var x (+ "hello" 10))
 */

// -----------------------------------------------
// Lexical grammar (tokens):

%lex

%%

\s+               %empty

\"[^\"]*\"        STRING

\d+               NUMBER

[\w\-+*=<>/]+     SYMBOL

/lex

// -----------------------------------------------
// Syntactic grammar (BNF):

%{

#include <string>
#include <initializer_list>

/**
 * Expression type.
 */
enum class ExpType {
  Number,
  String,
  Symbol,
  List,
};

/**
 * Expression.
 */
class Exp {
 public:
  ExpType type;

  int number;
  std::string string;
  std::vector<std::shared_ptr<Exp>> list;

  // Numbers:
  Exp(int number) : type(ExpType::Number), number(number) {}

  // Strings, Symbols:
  Exp(std::string& strVal) {
    if (strVal[0] == '"') {
      type = ExpType::String;
      string = strVal.substr(1, strVal.size() - 2);
    } else {
      type = ExpType::Symbol;
      string = strVal;
    }
  }

  // Lists:
  Exp(std::vector<std::shared_ptr<Exp>> list) : type(ExpType::List), list(list) {}

  // List init:
  Exp(const std::initializer_list<std::shared_ptr<Exp>> &list) : type(ExpType::List), list(list) {}

  ~Exp() {}
};

using Value = std::shared_ptr<Exp>;

%}

%%

Exp
  : Atom
  | List
  ;

Atom
  : NUMBER { $$ = std::make_shared<Exp>(std::stoi($1)) }
  | STRING { $$ = std::make_shared<Exp>($1) }
  | SYMBOL { $$ = std::make_shared<Exp>($1) }
  ;

List
  : '(' ListEntries ')' { $$ = $2 }
  ;

ListEntries
  : ListEntries Exp { $1->list.push_back($2); $$ = $1 }
  | %empty          { $$ = std::make_shared<Exp>(std::vector<std::shared_ptr<Exp>>{}) }
  ;
